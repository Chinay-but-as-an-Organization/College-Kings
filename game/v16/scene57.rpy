# SCENE 57: Apes Living Room Grayson tells Cameron Sam needs to go
# Locations: Apes Frat House
# Characters: SAMANTHA (Outfit: 1), MC (Outfit: 2), GRAYSON (Outfit: 2), CAMERON (Outfit: 2)
# Time: Thursday Morning

label v16s57:
    scene v16s57_1 # TPP. Show MC (slightly shocked, mouth closed) enters through the Apes Frat House front door, the door is still open
    with vpunch
    sa "Ow! Fuck off!"

    scene v16s57_1a # TPP. Show Mc (exhausted expression, mouth closed, holding one hand to his forehead eyes are closed) shutting the front door.
    with dissolve

    u "*Sighs* (Am I gonna regret seeing what this is about?)"

    scene v16s57_2 # TPP. MC (no expression, mouth is closed, looking at Samantha) enters the living room. Samantha (angry expression, mouth is open, looking at Grayson) she is sat on the couch with her feet up on the table. Grayson (angry expression, mouth is closed, looking at Samantha) he is sat on the couch a little further away but sitting next to Samantha. Cameron (annoyed expression, mouth is closed, looking at his phone) is next to Grayson, furthest away from Samantha, with his phone in both hands
    with dissolve

    sa "Don't hit my legs, Grayson!"

    scene v16s57_2a # TPP. MC (no expression, mouth is closed, looking at Samantha) grabs a chair and set's it in front of the table and the couch. Samantha (angry expression, mouth is closed, looking at Grayson) she is sat on the couch with her feet up on the table. Grayson (angry expression, mouth is open, looking at Samantha) he is sat on the couch a little further away but sitting next to Samantha. Cameron (annoyed expression, mouth is closed, looking at his phone) is next to Grayson, furthest away from Samantha, with his phone in both hands
    with dissolve

    gr "Get them off my table then! I don't want your dirty-ass feet on my furniture!"

    scene v16s57_3 # FPP. Show just Samantha (angry expression, mouth is open, looking at Grayson) show just from her waist up for all v16s57_3 renders, sitting next to Grayson (not shown,) and furthest from Cameron (not shown)
    with dissolve

    sa "Screw you, they're not dirty!"

    scene v16s57_3a # FPP. Show just Samantha (no expression, mouth is open, looking at Cameron) show just from her waist up for all v16s57_3 renders, sitting next to Grayson (not shown,) and furthest from Cameron (not shown)
    with dissolve

    sa "Cam, will you tell him to stop?"

    scene v16s57_4 # FPP. Show just Cameron (annoyed expression, mouth open, rolling his eyes, head facing up to the ceiling) show just from his waist up for all v16s57_4 renders, putting his phone away, sitting on the couch, with his phone in both hands, sitting on the couch next to Grayson (not shown) and furthest away from Samntha (not shown)
    with dissolve

    ca "*Sighs*"

    scene v16s57_4a # FPP. Show just Cameron (annoyed expression, mouth open, looking at Samantha), sitting on the couch next to Grayson (not shown) and furthest away from Samntha (not shown)
    with dissolve

    ca "Take your feet off his table. It's obvious that you're annoying him on purpose."

    scene v16s57_3
    with dissolve

    sa "I'm not! He's just looking for reasons to get pissed off at me like always."

    scene v16s57_5 # FPP. Show just Grayson (slight smile, mouth is open, looking at MC) show just from his waist up for all v16s57_5 renders, sitting on the couch between Samantha (not shown,) and Cameron (not shown)
    with dissolve

    gr "Hey, [name]. You're closest to the door. Take the trash out, will you?"

    scene v16s57_5a # FPP. Show just Grayson (slight smile, mouth is closed, looking at MC) sitting on the couch between Samantha (not shown,) and Cameron (not shown)
    with dissolve

    u "What trash?"

    scene v16s57_5b # FPP. Show just Grayson (slight smile, mouth is open, looking at MC) pointing at Samantha (not shown,) sitting on the couch between Samantha (not shown,) and Cameron (not shown)
    with dissolve

    gr "This trash."

    scene v16s57_3b # FPP. Show just Samantha (angry expression, mouth is open, looking at Grayson)
    with dissolve

    sa "Good one, prick."

    scene v16s57_2b # TPP. MC (no expression, mouth is closed, looking at Samantha and Grayson) sitting in the chair he placed in front of the table and the couch. Samantha (angry expression, mouth is closed, looking at Grayson) flipping Grayson off, she is sat on the couch with her feet up on the table. Grayson (angry expression, mouth is closed, looking at Samantha) giving her the "up yours" pose he is sat on the couch a little further away but sitting next to Samantha. Cameron (annoyed expression, mouth is closed, looking into his hand) is next to Grayson, furthest away from Samantha, with his hand over his eyes and face
    with dissolve

    u "(Jeez, these two are at each other's throats today.)"

    scene v16s57_4b # FPP. Show just Cameron (no expression, mouth closed, looking at MC), sitting on the couch next to Grayson (not shown) and furthest away from Samntha (not shown)
    with dissolve

    u "How long has this been going on for?"

    scene v16s57_4c # FPP. Show just Cameron (no expression, mouth open, looking at MC), sitting on the couch next to Grayson (not shown) and furthest away from Samntha (not shown)
    with dissolve

    ca "Too fucking long."

    scene v16s57_2c # TPP. MC (no expression, mouth is closed, looking at Samantha) sitting in the chair he placed in front of the table and the couch. Samantha (angry expression, mouth is open, looking at Grayson) she has taken her feet off of the table and is puffing her chest out at Grayson with her arms wide open. Grayson (Proud/smug expression, mouth is closed, looking at Samantha) he is sat on the couch a little further away but sitting next to Samantha. Cameron (annoyed expression, mouth is closed, looking at Samantha) is next to Grayson, furthest away from Samantha
    with dissolve

    sa "There! Satisfied you crying bitch?"

    scene v16s57_5c # FPP. Show just Grayson (slight smile, mouth is closed, looking at Samantha) sitting on the couch between Samantha (not shown,) and Cameron (not shown)
    with dissolve

    gr "Yes. Actually, wait... what's that? Nope, the smell is still here. You need a fucking shower."

    scene v16s57_3b
    with dissolve

    sa "You're so fucking rude, I swear."

    scene v16s57_2d # TPP. MC (no expression, mouth is closed, looking at Samantha) sitting in the chair he placed in front of the table and the couch. Samantha (angry expression, mouth is closed, looking at Grayson) she has taken her feet off of the table, Grayson (Angry expression, mouth is open, looking at Cameron) he is no longer sitting and has stood up from his spot on the couch. Cameron (no expression, mouth is closed, looking at Grayson) is next to Grayson, furthest away from Samantha
    with dissolve

    gr "Okay Cameron, I'm serious. I want her out of here."

    scene v16s57_2e # TPP. MC (no expression, mouth is closed, looking at Samantha) sitting in the chair he placed in front of the table and the couch. Samantha (angry expression, mouth is closed, looking at Grayson) she has taken her feet off of the table, Grayson (Angry expression, mouth is closed, looking at Cameron) he is standing by the doorway to the living room, Cameron (no expression, mouth is open, looking at Grayson)
    with dissolve

    ca "*Sighs* Gray-"

    scene v16s57_6 # FPP. Show Grayson (angry expression, mouth is open, looking at Samantha) standing in the living room doorway, from MC's viewpoint from v16s57_2 renders after he has sat down
    with dissolve

    gr "This is a fucking frat! I'm done putting up with her shitty attitude."

    u "(It seriously never ends...)"

    gr "She doesn't even clean up after herself. She just leeches."

    gr "You should at least clean the place every once and a while. Earn your keep!"

    scene v16s57_3c # FPP. Show just Samantha (angry expression, mouth is open, looking at Grayson) adjust where Samantha is looking based off Grayson's new position from render v16s57_2e
    with dissolve

    sa "Just because I'm a girl, doesn't mean I was born to clean up after you."

    scene v16s57_6
    with dissolve

    gr "This is Grayson's house, Grayson's rules. And what Grayson says, goes."

    scene v16s57_3c
    with dissolve

    sa "Oh, wow. Now he's talking about himself in the third-person. *Laughs*"

    sa "I'd love to leave, buddy, but Cameron won't let me!"

    scene v16s57_6a # FPP. Show Grayson (angry expression, mouth is open, looking at Cameron) standing in the living room doorway, from MC's viewpoint from v16s57_2 renders after he has sat down
    with dissolve

    gr "Cameron! Do something!"

    scene v16s57_2f # TPP. MC (no expression, mouth is closed, looking at Samantha) sitting in the chair he placed in front of the table and the couch. Samantha (angry expression, mouth is closed, looking at Grayson) flipping off Grayson, she has taken her feet off of the table, Grayson's back is turned to the render leaving the living room, Cameron (annoyed expression, mouth is closed, looking at Samantha)
    with dissolve

    if v14_SamanthaDrugs: # -if SamDrugs
        scene v16s57_4d # FPP. Show just Cameron (no expression, mouth open, looking at Samantha)
        with dissolve

        ca "Sam, I can't let you go because I'm scared that you'll get hurt again. You can't look after yourself."

        scene v16s57_3d # FPP. Show just Samantha (annoyed expression, mouth is open, looking at Cameron)
        with dissolve

        sa "Here we go..."

        scene v16s57_4d
        with dissolve

        ca "I really want to trust you but you know too many losers who are into drugs, and you can't make good decisions on your own."

        scene v16s57_3e # FPP. Show just Samantha (annoyed expression, mouth is open, rolls her eyes)
        with dissolve

        pause 0.75

        scene v16s57_4a
        with dissolve

        ca "What am I supposed to do?"

    else: # -if SamSober
        scene v16s57_4e # FPP. Show just Cameron (slight smile, mouth open, looking at Samantha)
        with dissolve

        ca "Sam, you've been doing well lately. You're sober, I can see it."

        scene v16s57_3a
        with dissolve

        sa "Yeah. I am. I've been trying."

        scene v16s57_4f # FPP. Show just Cameron (full smile, mouth open, looking at Samantha)
        with dissolve

        ca "Seriously, I'm proud of you."

        scene v16s57_3f # FPP. Show just Samantha (slight smile, mouth is closed, looking at Cameron)
        with dissolve

        pause 0.75

        scene v16s57_3g # FPP. Show just Samantha (full smile, mouth is closed, looking at Mc) she has started to tear up slightly and is wiping a tear away from her eye
        with dissolve

        pause 0.75

    scene v16s57_4d
    with dissolve

    ca "I just... Worry that things can change so quickly. I mean-"

    ca "I'm sure you'll be fine on your own. But if I leave you alone and something happens, I don't know what I'd do."

    # -Regardless of SamDrugs or SamSober-

    scene v16s57_3h # FPP. Show just Samantha (no expression, mouth is closed, looking at Cameron)
    with dissolve

    u "(At least this didn't turn into a screaming match...)"

    scene v16s57_3a
    with dissolve

    sa "Just, trust me. I promise I'll be fine this time."

    scene v16s57_3h
    with dissolve

    menu:
        "Agree with Samantha":
            $ v16s57_agree_with_samantha = 1 # TODO: Variable # PlaceHolder Variable
            $ add_point(KCT.BOYFRIEND)
            $ add_point(KCT.TROUBLEMAKER)

            scene v16s57_4b
            with dissolve

            u "For what it's worth, Cameron, I think Samantha has learned her lesson by now."

            u "I think she's capable of living on her own. I think it's time to let her make her own decisions."

            scene v16s57_4g # FPP. Show just Cameron (slight smile, mouth open, looking at MC)
            with dissolve

            ca "Well thanks for your opinion, butt-munch. I'll take that under consideration."

            scene v16s57_3i # FPP. Show just Samantha (slight smile, mouth is open, looking at MC)
            with dissolve

            sa "Thanks, butt-munch."

        "Agree with Cameron":
            $ v16s57_agree_with_samantha = 2 # TODO: Variable  # PlaceHolder Variable
            $ add_point(KCT.BRO)
            $ add_point(KCT.TROUBLEMAKER)

            scene v16s57_3j # FPP. Show just Samantha (no expression, mouth is closed, looking at MC)
            with dissolve

            u "I have to agree with Cameron. You've been through a lot in the past, Sam."

            scene v16s57_3e
            with dissolve

            sa "Ugh... I know this already."

            scene v16s57_3j
            with dissolve

            u "He just wants to make sure you're in a good headspace when you leave."

            u "Which is why I think you should stay here for now. Just a little while longer."

            scene v16s57_3k # FPP. Show just Samantha (slightly annoyed expression, mouth is open, looking at MC)
            with dissolve

            sa "But Grayson's awful!"

            scene v16s57_4a
            with dissolve

            ca "Stop arguing with him then! Just stay out of his way as much as possible."

            scene v16s57_3a
            with dissolve

            sa "*Sighs* Okay, I'll try."

        "Stay out of it":
            scene v16s57_3h
            with dissolve

            u "(No way am I taking sides in this. Just going to remain as a casual bystander...)"

    # -Regardless of who MC agrees with-

    scene v16s57_4e
    with dissolve

    ca "Once I'm sure I can trust you again, I'll be the one helping you pack your bags. I promise."

    scene v16s57_3l # FPP. Show just Samantha (slight smile, mouth is open, looking at Cameron)
    with dissolve

    sa "Haha, okay. I'll do my best to prove that I can be independent."

    sa "Even if I do want to punch Grayson in the mouth."

    scene v16s57_4e
    with dissolve

    ca "Ha, fair."

    scene v16s57_3f
    with dissolve

    u "(Jeez, that's the closest thing to a happy family moment this house has seen in a long time.)"

    scene v16s57_7 # FPP. Show both Samantha (slight smile, mouth is closed, looking at MC) sitting in her spot from v16s57_2 renders and Cameron (slight smile, mouth is closed, looking at MC) sitting in her spot from v16s57_2 renders and Cameron
    with dissolve

    u "Not that this hasn't been fun, but I'm going to leave you guys to it. Enjoy your bonding experience, haha. Catch you later."

    scene v16s57_7a # FPP. Show both Samantha (slight smile, mouth is open, looking at MC) sitting in her spot from v16s57_2 renders and Cameron (slight smile, mouth is closed, looking at MC) sitting in her spot from v16s57_2 renders and Cameron
    with dissolve

    sa "Okay, [name]. Be good."

    scene v16s57_7b # FPP. Show both Samantha (slight smile, mouth is closed, looking at MC) sitting in her spot from v16s57_2 renders and Cameron (slight smile, mouth is open, looking at MC) sitting in her spot from v16s57_2 renders and Cameron
    with dissolve

    ca "Later, dickweed."

    if samantha.relationship >= Relationship.FWB:# -samanthaRS winks at mc on his way out, Cameron sees it, mc can hear them arguing on his way out
        scene v16s57_7c # FPP. Show both Samantha (slight smile, mouth is closed, winking at MC) sitting in her spot from v16s57_2 renders and Cameron (confused expression, mouth is closed, looking at Samantha) sitting in her spot from v16s57_2 renders and Cameron
        with dissolve

        pause 0.75

        scene v16s57_2g # TPP. MC (slight smile, mouth is closed, facing the doorway) walking out of the living room, Samantha (playful guilty expression, mouth is closed, looking at Cameron), she has taken her feet off of the table, Cameron (annoyed expression, mouth is closed, looking at Samantha) hands up turned up in front of him like "what are you doing" towards Samantha, Grayson is no longer in the render
        with dissolve

        pause 0.75 

        scene v16s57_8 # FPP. Show just a close up side view of MC's face (nervous expression, mouth is closed)
        with dissolve

        ca "Did you just wink at him, Sam?"

        scene v16s57_8a # FPP. Show just a close up side view of MC's face (slight smile, mouth is closed)
        with dissolve

        sa "*Chuckles* I think you're seeing things..."

    # -continue regardless

    scene v16s57_9 # TPP. Show just MC (slight smile, mouth is closed) leavng the front door of the Apes Frat House
    with dissolve

    pause 0.75

    if v14_help_lindsey: # -if helping Lindsey, transition to Scene 58-
        jump v16s58

    else: # -if not helping Lindsey, transition to Scene 61-
        jump v16s61